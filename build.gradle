plugins {
    id 'java'
    id 'signing'
    id 'maven-publish'
    id 'io.freefair.lombok' version '6.4.0'
    id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
}

group 'net.insprill'
version '1.3.0'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8
compileJava.options.encoding = 'UTF-8'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

test {
    useJUnitPlatform()
}

java {
    withSourcesJar()
    withJavadocJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            pom {
                name = project.name
                description = 'A light-weight Fetch-like library for Java'
                url = 'https://github.com/Insprill/fetch4j'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'insprill'
                        name = 'Pierce Thompson'
                        url = 'https://github.com/Insprill'
                    }
                }
                scm {
                    url = 'https://github.com/Insprill/fetch4j'
                    connection = 'scm:git:git://github.com/Insprill/fetch4j.git'
                    developerConnection = 'scm:git:git@github.com:Insprill/fetch4j.git'
                }
                issueManagement {
                    system = 'GitHub Issues'
                    url = 'https://github.com/Insprill/fetch4j/issues'
                }
            }
        }
    }
}

signing {
    useGpgCmd()
    sign(publishing.publications)
}

nexusPublishing {
    repositories {
        sonatype {
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
        }
    }
}
